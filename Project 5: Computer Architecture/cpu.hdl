/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */

CHIP CPU {
    IN  inM[16],         
        instruction[16], 
        reset;         

    OUT outM[16],        
        writeM,          
        addressM[15],    
        pc[15];         

    PARTS:
    ALU(x=outx, y=outy , zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=ALUout , zr=zrout, ng=ngout);
    Mux16(a=areg , b=inM , sel=instruction[12] , out=outy);
    Not(in=instruction[15] , out=isainstruction); 
    And(a=instruction[15], b=instruction[4] , out=iscinstruction); 
    And(a=instruction[15], b=instruction[5], out=instructionloadA);
    Or(a=isainstruction , b=instructionloadA , out=loadareg );
    Mux16(a=instruction , b=ALUout, sel=instruction[15] , out=inp );
    ARegister(in=inp, load=loadareg, out=areg ); 
    DRegister(in=ALUout , load=iscinstruction , out=outx ); 
    And(a=instruction[15], b=instruction[3], out=writeM);
    Or16(a=false, b=ALUout, out=outM);
    Or16(a=false, b=areg, out[0..14]=addressM);
    Not(in=zrout, out=notZR);
    Not(in=ngout, out=notNG);
    And(a=notZR, b=notNG, out=isPositive); 
    And(a=instruction[0], b=isPositive, out=jumpPos); 
    And(a=instruction[1], b=zrout, out=jumpZero);    
    And(a=instruction[2], b=ngout, out=jumpNeg);     
    Or(a=jumpPos, b=jumpZero, out=jumpEQorGT);
    Or(a=jumpEQorGT, b=jumpNeg, out=jumpConditionsMet);
    And(a=instruction[15], b=jumpConditionsMet, out=loadPC);
    Not(in=loadPC, out=incPC); 
    PC(in=areg, load=loadPC, inc=incPC, reset=reset, out[0..14]=pc);
}